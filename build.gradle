apply plugin: "groovy"
apply plugin: "eclipse"

repositories {
    jcenter()
}

ext {
	drivers = ["firefox", "chrome"]
}

drivers.each { driver ->
	task "${driver}Test"(type: Test) {
		reports {
			html.destination = reporting.file("$name/html")
		}

		outputs.upToDateWhen { false }  // Always run tests

		systemProperty "geb.build.reportsDir", reporting.file("$name/geb")
		systemProperty "geb.env", driver
	}
}

dependencies {
	testCompile "org.gebish:geb-core:1.1.1"
	testCompile "org.gebish:geb-junit4:1.1.1"
	testCompile "org.gebish:geb-spock:1.1.1"
	testCompile("org.spockframework:spock-core:1.0-groovy-2.4") {
		exclude group: "org.codehaus.groovy-all"
	}
	testCompile "org.codehaus.groovy:groovy-all:2.4.10"
	testCompile "org.seleniumhq.selenium:selenium-firefox-driver:3.3.1"
	testCompile "org.seleniumhq.selenium:selenium-chrome-driver:3.3.1"
	testCompile "org.seleniumhq.selenium:selenium-support:2.52.0"
}

test {
	systemProperties "geb.build.reportsDir": "$reportsDir/geb"
}

groovydoc {
	source = "src/test/groovy"
	docTitle = "Example of localized Geb tests"
	classpath = sourceSets.test.runtimeClasspath
}